prog = ws decl*:d stat:b { NemoProgram(:d, :b)};
decl = "var" ws id:v ":" ws type:t ";" ws { NemoDeclaration(:v, :t) };

type  = int | array;
int   = "int" ws { NemoIntType() };
array = "[" ws type:t "]" ws { NemoArrType(:t) };

stat   = print | test | seq | choice | loop | assign | ifelse | while;
test   = "(" ws expr:e1 cond$s ws expr:e2 ")" ws "?" ws { NemoTest($s, :e1, :e2) };
seq    = "{" ws stat:s1 ";" ws stat:s2 seqs*:ss "}" ws { createSeq(:s1, :s2, :ss) };
seqs   = ";" ws stat;
choice = "{" ws stat:s1 "U" ws stat:s2 chs*:ss "}" ws { createChoice(:s1, :s2, :ss) };
chs    = "U" ws stat;
loop   = "*" ws stat:b { NemoLoop(:b) };
print  = "print" ws "(" ws expr:e ")" ws { NemoPrint(:e) };
assign = id:v ":=" ws expr:e { NemoAssign(:v, :e) };
ifelse = "if"    ws "(" ws expr:e1 cond$s ws expr:e2 ")" ws stat:s1 "else" ws stat:s2 { createIfElse($s, :e1, :e2, :s1, :s2) };
while  = "while" ws "(" ws expr:e1 cond$s ws expr:e2 ")" ws stat:s1 { createWhile($s, :e1, :e2, :s1) };

expr   = const | bin_op | un_op | update | apply | var;
const  = digit+ $s ws { NemoInt(s2i($s)) };
bin_op = "(" ws expr:l ("+"|"*"|"/"|"-"|"%") $s ws expr:r ")" ws { NemoBinOp($s, :l, :r) };
un_op  = "-" $s ws expr:e { NemoUnOp($s, :e) };
apply  = "app" ws "(" ws expr:a "," ws expr:i ")" ws { NemoApp(:a, :i) };
update = "upd" ws "(" ws expr:a "," ws expr:i "," ws expr:v ")" ws { NemoUpd(:a, :i, :v) };
var    = id:v { NemoVar(:v) };

cond   = "!=" | "==" | "<=" | ">=" | "<" | ">" ;

id = letter+ $s ws { $s };
letter = 'a'-'z';
digit  = '0'-'9';
ws = (' ' | '\t' | '\n' | '\r')*;
